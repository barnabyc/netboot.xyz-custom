#!ipxe

set win_url http://boot/winpe/x64

:custom
clear custom_choice
menu Happy custom thing land
item --gap Windows Installers
item rock_and_roll ${space} Automatically install Windows 10 x64
choose custom_choice || goto custom_exit
echo ${cls}
goto ${custom_choice}
goto custom_exit

:rock_and_roll
kernel http://${boot_domain}/wimboot
initrd ${win_url}/bootmgr bootmgr
initrd ${win_url}/boot/bcd bcd
initrd ${win_url}/boot/boot.sdi boot.sdi
initrd ${win_url}/sources/boot.wim boot.wim
md5sum wimboot
iseq ${img_sigs_enabled} true && goto verify_sigs || goto skip_sigs
:verify_sigs
echo
echo Checking wimboot signature...
imgverify wimboot ${sigs}wimboot.sig || goto error
:skip_sigs
boot

:custom_exit
exit
